buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('keystore.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.tiknil.boilerplatemvvm"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            manifestPlaceholders =
                    [
                        fabricApiKey: "",
                        googleApiKey : "",
                    ]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            manifestPlaceholders =
                    [
                            fabricApiKey: "c8a652de46b4ef3c480937b446b20eb781852c51",
                            googleApiKey : "",
                    ]
            debuggable true
            minifyEnabled true
            useProguard false
            proguardFiles fileTree('proguard').asList().toArray()
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def supportLibraryVersion = '27.0.0'
def googlePlayVersion = '11.6.0'
def retrofitVersion = '2.3.0'
def rxbinding2Version = '2.0.0'
def okHttpClientVersion = '3.6.0'
def lifecycleVersion = '1.0.0'
def daggerVersion = '2.12'
def butterKnifeVersion = '8.8.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support

    // Per poter utilizzare dei componenti gestiti da tutti gli smartphone con versioni Android non recenti (https://developer.android.com/topic/libraries/support-library/index.html?hl=id)
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:support-compat:${supportLibraryVersion}"

    // Per avere una versione migliore e avanzata di ListView (https://developer.android.com/training/material/lists-cards.html)
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"

    // Sono degli elementi base avanzati proposti da Google per migliorare il feeling dell'utente tra le varie app
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support:percent:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"

    // Google Play Services
    compile "com.google.android.gms:play-services-location:${googlePlayVersion}"
    compile "com.google.android.gms:play-services-places:${googlePlayVersion}"
    compile "com.google.android.gms:play-services-maps:${googlePlayVersion}"

    // EventBus per la comunicazione interna tramite eventi
    compile 'org.greenrobot:eventbus:3.0.0'

    // Libreria per semplificazione log
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Retrofit2 - richieste HTTP con supporto Rx (Reactive)
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpClientVersion}"

    // Rx Android (Reactive)
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'
    compile 'com.cantrowitz:rxbroadcast:2.0.0'
    compile 'com.google.guava:guava:22.0'

    // Implementazione Rx (Reactive) degli elementi UI di Android
    compile "com.jakewharton.rxbinding2:rxbinding:${rxbinding2Version}"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:${rxbinding2Version}"
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${rxbinding2Version}"
    compile "com.jakewharton.rxbinding2:rxbinding-design:${rxbinding2Version}"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:${rxbinding2Version}"

    // Libreria per bindare gli observable in base al lifecycle di Activity e Fragment
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'

    //Viewmodel
    compile "android.arch.lifecycle:extensions:${lifecycleVersion}"
    annotationProcessor "android.arch.lifecycle:compiler:${lifecycleVersion}"

    // Dagger 2

    // Dagger core dependencies
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"

    // Dagger Android dependencies
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"
    compile "com.google.dagger:dagger-android:${daggerVersion}"

    // Dagger Android per l'injection di activity e fragment
    compile "com.google.dagger:dagger-android-support:${daggerVersion}"

    // Butterknife per il binding degli elementi grafici nelle classi
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    //Librerie per Unit Tests
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
}
